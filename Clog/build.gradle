apply plugin: "java"
apply plugin: "jacoco"
apply plugin: "idea"
apply plugin: "com.eden.orchidPlugin"
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

// Configure Project
//----------------------------------------------------------------------------------------------------------------------

project.group = rootProject.group
project.version = rootProject.version
project.description = 'Simple yet powerful logging and string formatting for Java'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.+'

    compile 'org.fusesource.jansi:jansi:1.14'

    orchidCompile "io.github.javaeden.orchid:OrchidCore:${orchid_version}"
    orchidCompile "io.github.javaeden.orchid:OrchidBsDoc:${orchid_version}"
    orchidCompile "io.github.javaeden.orchid:OrchidPages:${orchid_version}"
    orchidCompile "io.github.javaeden.orchid:OrchidJavadoc:${orchid_version}"
    orchidCompile "io.github.javaeden.orchid:OrchidPluginDocs:${orchid_version}"
    orchidCompile "io.github.javaeden.orchid:OrchidWiki:${orchid_version}"
    orchidCompile "io.github.javaeden.orchid:OrchidChangelog:${orchid_version}"
    orchidCompile "io.github.javaeden.orchid:OrchidSyntaxHighlighter:${orchid_version}"
}

// Javadoc and Orchid
//----------------------------------------------------------------------------------------------------------------------

orchid {
    version = "${project.version}"
    theme = "BsDoc"
    baseUrl = "https://javaeden.github.io/Clog"

    if(project.hasProperty('env') && project.property('env') == 'prod') {
        environment = "production"
    }
    else {
        environment = "debug"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from project.sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: orchidBuild) {
    classifier = 'javadoc'
    from files(project.buildDir.absolutePath + '/docs/orchid')
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

project.tasks.javadoc.onlyIf { false }
project.tasks.assemble.dependsOn orchidBuild
project.tasks.orchidBuild.mustRunAfter javadoc

// Jacoco Code Coverage
//----------------------------------------------------------------------------------------------------------------------

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
check.dependsOn jacocoTestReport

// Publish to Bintray
//----------------------------------------------------------------------------------------------------------------------

afterEvaluate { project ->
    def pomConfig = {
        scm {
            url 'https://github.com/JavaEden/Clog.git'
            connection 'https://github.com/JavaEden/Clog.git'
            developerConnection 'https://github.com/JavaEden/Clog.git'
        }
        licenses {
            license {
                name 'MIT'
                url 'https://opensource.org/licenses/mit'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'JavaEden'
                name 'Casey Brooks'
                email 'cjbrooks12@gmail.com'
            }
        }
    }

    project.publishing {
        publications {
            ClogPublication(MavenPublication) {
                from project.components.java
                artifact project.sourcesJar
                artifact project.javadocJar
                groupId "${project.group}"
                artifactId "${project.name}"
                version "${project.version}"
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', project.description)
                    root.appendNode('name', 'Clog')
                    root.appendNode('url', "https://javaeden.github.io/Clog/${project.name}")
                    root.children().last() + pomConfig
                }
            }
        }
    }

    bintray {
        user = "${project.property('bintray_username')}"
        key = "${project.property('bintray_apiKey')}"
        publications = ['ClogPublication']

        dryRun = project.hasProperty('dryDeploy')
        publish = !project.hasProperty('dryDeploy')
        override = true

        pkg {
            repo = "Eden"
            name = "${project.name}"
            userOrg = 'javaeden'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/JavaEden/Clog.git'

            version {
                name = "${project.version}"
                desc = "${project.name} ${project.version}"
                released = new Date()

                gpg {
                    sign = true
                }
                mavenCentralSync {
                    sync = (project.hasProperty('includeMavenCentralSync'))
                    user = "${project.property('mavenCentral_username')}"
                    password = "${project.property('mavenCentral_password')}"
                }
            }
        }
    }
}

project.tasks.publish.dependsOn bintrayUpload
project.tasks.publish.dependsOn orchidDeploy


task deploy {
    dependsOn project.tasks.publish
    doLast { }
}
